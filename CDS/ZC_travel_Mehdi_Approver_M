@EndUserText.label: 'Approver Projection Travel'
@AccessControl.authorizationCheck: #NOT_REQUIRED
@UI: {
  headerInfo: { typeName: 'Travel',
               typeNamePlural: 'Travels',
               title: { type: #STANDARD, value: 'TravelID' }
               } }
@Search.searchable: true

define root view entity ZC_travel_Mehdi_Approver_M
  provider contract transactional_query
  as projection on ZI_travel_Mehdi_M
{

      @UI.facet: [ { id:              'Travel',
                           purpose:         #STANDARD,
                           type:            #IDENTIFICATION_REFERENCE,
                           label:           'Travel',
                           position:        10 } ,
                         { id:              'Booking',
                           purpose:         #STANDARD,
                           type:            #LINEITEM_REFERENCE,
                           label:           'Booking',
                           position:        20,
                           targetElement:   '_booking'}]
      @UI: {
                lineItem:       [ { position: 10, importance: #HIGH } ],
                identification: [ { position: 10 } ] }
      @Search.defaultSearchElement: true

  key TravelId,
      @UI: {
              lineItem:       [ { position: 20, importance: #HIGH } ],
              identification: [ { position: 20 } ],
              selectionField: [ { position: 20 } ] }
      @Consumption.valueHelpDefinition: [ { entity : {name: '/DMO/I_Agency',
                                           element: 'AgencyID'  } } ]
      @ObjectModel.text.element: ['AgencyName']
      @Search.defaultSearchElement: true
      AgencyId,
      _agency.Name       as AgencyName,
      @UI:{ lineItem: [{ position: 30, importance: #HIGH }],
            selectionField: [{ position: 30 }],
            identification: [{ position: 30 }]}
      @Search.defaultSearchElement: true
      @Consumption.valueHelpDefinition: [{ entity:{name: '/DMO/I_Customer', element: 'CustomerID'  } }]
      @ObjectModel.text.element: [ 'CustomerName' ]
      CustomerId,
      _customer.LastName as CustomerName,
      @UI:{ lineItem: [{ position: 40 } ] }
      BeginDate,
      @UI:{ lineItem: [{ position: 41 }]}
      EndDate,
      @UI:{ lineItem: [{ position: 42, importance: #MEDIUM }],
            selectionField: [{ position: 42 }],
            identification: [{ position: 42 }]}
      @Semantics.amount.currencyCode: 'CurrencyCode'
      BookingFee,
      @UI:{ lineItem: [{ position: 43, importance: #MEDIUM }],
            selectionField: [{ position: 43 }],
            identification: [{ position: 43 }]}
      @Semantics.amount.currencyCode: 'CurrencyCode'
      TotalPrice,
      @Consumption.valueHelpDefinition: [{ entity:{name: 'I_Currency', element: 'Currency'  } }]
      CurrencyCode,
      @UI:{ lineItem: [{ position: 45, importance: #MEDIUM }],
            identification: [{ position: 45 }]}
      Description,
      @UI:{ lineItem:       [{ position: 15, importance:  #HIGH },
                             { type: #FOR_ACTION, dataAction: 'acceptTravel' ,label: 'Accept Travel' },
                             { type: #FOR_ACTION, dataAction: 'rejectTravel' ,label: 'Reject Travel' }
                             ],
            identification: [{ position: 15 },
                             { type: #FOR_ACTION, dataAction: 'acceptTravel' ,label: 'Accept Travel' },
                             { type: #FOR_ACTION, dataAction: 'rejectTravel' ,label: 'Reject Travel' }
                             ],      
            selectionField: [{ position: 40 }],      
            textArrangement: #TEXT_ONLY}
      @EndUserText.label: 'Overall Status'
      @Consumption.valueHelpDefinition: [{ entity:{name: '/DMO/I_Overall_Status_VH', element: 'OverallStatus'  } }]
      @ObjectModel.text.element: [ 'OverallStatusText' ]      
      OverallStatus,
      @UI.hidden: true
      _status._Text.Text as OverallStatusText : localized,
      @UI.hidden: true
      CreatedBy,
      @UI.hidden: true
      CreatedAt,
      @UI.hidden: true
      LastChangedBy,
      @UI.hidden: true
      LastChangedAt,
      /* Associations */
      _agency,
      _booking : redirected to composition child ZC_booking_Mehdi_Approver_M,
      _currency,
      _customer,
      _status
}
